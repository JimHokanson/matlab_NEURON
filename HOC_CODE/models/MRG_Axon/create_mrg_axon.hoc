//Written to work with Matlab code
//create_mrg_axon

create node[axonnodes], MYSA[paranodes1], FLUT[paranodes2], STIN[axoninter]
access node[0]


proc MRG__init_axon() {
	for i=0,axonnodes-1 {
		node[i]{
			//node_list.append()
			nseg = 1
			diam = node_diameter
			L    = node_length
			Ra   = rho_axial_node
			cm   = cap_nodal
			insert axnode			
			insert extracellular 
				xraxial = xraxial_node
				xg      = xg_node 
				xc      = xc_node
			}
		}
	for i=0, paranodes1-1 {
		MYSA[i]{
			nseg = 1
			diam = fiber_diameter
			L    = paranode_length_1
			Ra   = rho_axial_1
			cm 	 = cm_1
			insert pas
				g_pas = g_pas_1		
				e_pas = v_init
			insert extracellular 
				xraxial = xraxial_1 
				xg      = xg_1 
				xc		= xc_1
			}
	}
	for i=0, paranodes2-1 {
		FLUT[i]{
			nseg = 1
			diam = fiber_diameter
			L    = paranode_length_2
			Ra	 = rho_axial_2
			cm 	 = cm_2
			insert pas
				g_pas = g_pas_2	
				e_pas = v_init
			insert extracellular 
				xraxial = xraxial_2 
				xg 	    = xg_2 
				xc 		= xc_2
			}
	}
	for i=0, axoninter-1 {
		STIN[i]{
			nseg 	= 1
			diam 	= fiber_diameter
			L	    = stin_seg_length
			Ra 		= rho_axial_i
			cm 		= cm_i
			insert pas 
				g_pas = g_pas_i 
				e_pas = v_init
			insert extracellular 
				xraxial = xraxial_i 
				xg 		= xg_i 
				xc 		= xc_i
			}
	}
	for i=0, axonnodes-2 {
		connect MYSA[2*i](0), node[i](1)
		connect FLUT[2*i](0), MYSA[2*i](1)
		connect STIN[6*i](0), FLUT[2*i](1)
		connect STIN[6*i+1](0), STIN[6*i](1)
		connect STIN[6*i+2](0), STIN[6*i+1](1)
		connect STIN[6*i+3](0), STIN[6*i+2](1)
		connect STIN[6*i+4](0), STIN[6*i+3](1)	
		connect STIN[6*i+5](0), STIN[6*i+4](1)	
		connect FLUT[2*i+1](0), STIN[6*i+5](1)
		connect MYSA[2*i+1](0), FLUT[2*i+1](1)
		connect node[i+1](0), MYSA[2*i+1](1)	
		}
}
MRG__init_axon()